# Variables for file 
dim port
dim led_value
dim read_result
dim connected 
dim mac_addr(6)
dim tmp(20)
dim command
dim dfu_pointer

# Constants
const TIMER_SECONDS = 1
const TICKS_PER_SECOND = 32768

const LED_PIN = $01  # P0_0
const LED_PORT = 0

const MAIN_TIMER_HANDLE = 0

# System Boot event listener
event system_boot(major, minor, patch, build, ll_version, protocol_version, hw)

    # Get local BT address
    call system_address_get()(mac_addr(0:6))

    # Set the Serial Number and device name
    tmp(0:1) = (mac_addr(5:1)/$10) + 48 + ((mac_addr(5:1)/$10)/10*7)
    tmp(1:1) = (mac_addr(5:1)&$f) + 48 + ((mac_addr(5:1)&$f )/10*7)
    tmp(2:1) = ":"
    tmp(3:1) = (mac_addr(4:1)/$10) + 48 + ((mac_addr(4:1)/$10)/10*7)
    tmp(4:1) = (mac_addr(4:1)&$f) + 48 + ((mac_addr(4:1)&$f )/10*7)
    tmp(5:1) = ":"
    tmp(6:1) = (mac_addr(3:1)/$10) + 48 + ((mac_addr(3:1)/$10)/10*7)
    tmp(7:1) = (mac_addr(3:1)&$f) + 48 + ((mac_addr(3:1)&$f )/10*7)
    tmp(8:1) = ":"
    tmp(9:1) = (mac_addr(2:1)/$10) + 48 + ((mac_addr(2:1)/$10)/10*7)
    tmp(10:1) = (mac_addr(2:1)&$f) + 48 + ((mac_addr(2:1)&$f )/10*7)
    tmp(11:1) = ":"
    tmp(12:1) = (mac_addr(1:1)/$10) + 48 + ((mac_addr(1:1)/$10)/10*7)
    tmp(13:1) = (mac_addr(1:1)&$f) + 48 + ((mac_addr(1:1)&$f )/10*7)
    tmp(14:1) = ":"
    tmp(15:1) = (mac_addr(0:1)/$10)+ 48 + ((mac_addr(0:1)/$10)/10*7)
    tmp(16:1) = (mac_addr(0:1)&$f) + 48 + ((mac_addr(0:1)&$f )/10*7)

    # Write 12-byte serial+MAC value to "Serial Number" characteristic
    call attributes_write(xgatt_serial_number, 0, 17, tmp(0:17))

    # Set device to advertisement mode and allow undirected connections
    call gap_set_mode(gap_general_discoverable, gap_undirected_connectable)

    # Configure P0.0 as output
    call hardware_io_port_config_direction(LED_PORT, LED_PIN)
 
    # Set timer to generate event every 1s (0 is repeating, 1 is single-shot for final parameter)
    call hardware_set_soft_timer(TIMER_SECONDS * TICKS_PER_SECOND, MAIN_TIMER_HANDLE, 0)

    led_value = 0    
    connected = 0
end


# Timer event listener toggles LED at a certain rate
event hardware_soft_timer(handle)
    call hardware_io_port_read(LED_PORT, LED_PIN)(read_result, port, led_value)

    if (led_value & LED_PIN) then
        call hardware_io_port_write(LED_PORT, LED_PIN, 0)
    else
        call hardware_io_port_write(LED_PORT, LED_PIN, LED_PIN)
    end if
end


# Handles OTA Control Point Attribute (commands) and OTA Data Attribute (firmware update) writes
# and performs the necessary actions
procedure handle_ota_control(connection, offset, value_len, value_data())
    # Check if OTA control point attribute is written by the remote device and execute the command
        # Command 0 : Erase flash block 0 (0x0-0x1FFFF)
        # Command 1 : Erase flash block 1 (0x10000-0x3FFFF)
        # Command 2 : Reset DFU data pointer
        # Command 3 : Boot to DFU mode
        # Command 4 : Power up external flash
    # In case of errors application error code 0x80 is returned to the remote device
    # In case the flash comms fails error code 0x90 is returned to the remote device
    
    # Attribute is user attribute, reason is always write_request_user
    if value_len > 1 || offset > 0 then
        # Not a valid command -> report application error code : 0x80
        call attributes_user_write_response(connection, $80)
    else
        command = value_data(0:1)

        if command > 4 then # Unknown command -> report application error code : 0x80
            call attributes_user_write_response(connection, $80)
        else
            if command = 3 then # Command 3 received -> Boot to DFU mode
                call system_reset(1)
            else
                # Other commands are not used, but still accepted in order
                # to be compatible with the external flash OTA
                # implementation
                call attributes_user_write_response(connection, $0)
            end if
        end if
    end if
end


# Incoming data event listener
event attributes_value(connection, reason, handle, offset, value_len, value_data)
    # Both ota_control endpoints run the same code, however, the wo_response just ignores most of this
    if handle = xgatt_ota_control || handle = xgatt_ota_control_wo_response then
        call handle_ota_control(connection, offset, value_len, value_data(0:value_len))
    end if

    # Check if OTA data attribute is written which carries the firmware update
    # and store the data to the internal flash
    if handle = xgatt_ota_data || handle = xgatt_ota_data_w_response then
        call flash_write_data(dfu_pointer, value_len, value_data(0:value_len))
        dfu_pointer = dfu_pointer + value_len
    end if
end


# Connection event listener
event connection_status(connection, flags, address, address_type, conn_interval, timeout, latency, bonding)
    if connected = 0 then
        # Set up a connection interval of between 6*1.25ms to 10*1.25ms (7.5-12.5ms)
        # Android (and iOS?) respond to connection interval updates, but cannot set them
        call connection_update(connection, 6, 10, latency, timeout)
    end if
    connected = 1
end


# Disconnection event listener
event connection_disconnected(handle, result)
    connected = 0
    # Makes the device visible and connectable
    call gap_set_mode(gap_general_discoverable, gap_undirected_connectable)
end
