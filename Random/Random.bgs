# OTA Variables
dim command
dim dfu_pointer  # current pointer to flash location to upload firmware
dim erase_page   # current page to erase
const MAX_ERASE_PAGE = 64

dim port
dim led_value
dim read_result

dim connected 
dim tmp(20)
dim mac_addr(6)
dim seed

const TICKS_PER_SECOND = 32678
const TIMER_SECONDS = 1
const MAIN_TIMER_HANDLE = 0

import "rand.bgs"

# Handles OTA Control Point Attribute (commands) and OTA Data Attribute (firmware update) writes
# and performs the necessary actions
procedure handle_ota_control(connection, offset, value_len, value_data())
    # Check if OTA control point attribute is written by the remote device and execute the command
        # Command 0 : Erase flash block 0 (0x0-0x1FFFF)
        # Command 1 : Erase flash block 1 (0x10000-0x3FFFF)
        # Command 2 : Reset DFU data pointer
        # Command 3 : Boot to DFU mode
        # Command 4 : Power up external flash
    # In case of errors application error code 0x80 is returned to the remote device
    # In case the flash comms fails error code 0x90 is returned to the remote device
    
    # Attribute is user attribute, reason is always write_request_user
    if value_len > 1 || offset > 0 then
        # Not a valid command -> report application error code : 0x80
        call attributes_user_write_response(connection, $80)
    else
        command = value_data(0:1)

        if command > 4 then # Unknown command -> report application error code : 0x80
            call attributes_user_write_response(connection, $80)
        else
            if command = 3 then # Command 3 received -> Boot to DFU mode
                call system_reset(1)
            else
                # Other commands are not used, but still accepted in order
                # to be compatible with the external flash OTA
                # implementation
                call attributes_user_write_response(connection, $0)
            end if
        end if
    end if
end


# Incoming data event listener
event attributes_value(connection, reason, handle, offset, value_len, value_data)
    
    if (handle = device_reset) then
        command=value_data(0:1)
        # Command 1 received, reset device
        if command=1 then  
            call system_reset(0)
        end if
    end if
    
	# Both ota_control endpoints run the same code, however, the wo_response just ignores most of this
    if handle = ota_control || handle = ota_control_wo_response then
        call handle_ota_control(connection, offset, value_len, value_data(0:value_len))
    end if

    # Check if OTA data attribute is written which carries the firmware update
    # and store the data to the internal flash
    if handle = ota_data || handle = ota_data_w_response then
        call flash_write_data(dfu_pointer, value_len, value_data(0:value_len))
        dfu_pointer = dfu_pointer + value_len
    end if
end


event system_boot(major, minor, patch, build, ll_version, protocol_version, hw)
    # Erase internal flash dedicated for OTA
    # For this to work, the script timeout has to be increased from the default
    # value in application configuration file (config.xml)
    erase_page = 0
    while erase_page < MAX_ERASE_PAGE
        call flash_erase_page(erase_page)
        erase_page = erase_page + 1
    end while

    # Set device to advertisement mode and allow undirected connections
    call gap_set_mode(gap_general_discoverable, gap_undirected_connectable)
    
    #Get local BT address
    call system_address_get()(mac_addr(0:6))

    # Set the Serial Number and device name
    tmp(0:1) = (mac_addr(5:1)/$10) + 48 + ((mac_addr(5:1)/$10)/10*7)
    tmp(1:1) = (mac_addr(5:1)&$f) + 48 + ((mac_addr(5:1)&$f )/10*7)
    tmp(2:1) = ":"
    tmp(3:1) = (mac_addr(4:1)/$10) + 48 + ((mac_addr(4:1)/$10)/10*7)
    tmp(4:1) = (mac_addr(4:1)&$f) + 48 + ((mac_addr(4:1)&$f )/10*7)
    tmp(5:1) = ":"
    tmp(6:1) = (mac_addr(3:1)/$10) + 48 + ((mac_addr(3:1)/$10)/10*7)
    tmp(7:1) = (mac_addr(3:1)&$f) + 48 + ((mac_addr(3:1)&$f )/10*7)
    tmp(8:1) = ":"
    tmp(9:1) = (mac_addr(2:1)/$10) + 48 + ((mac_addr(2:1)/$10)/10*7)
    tmp(10:1) = (mac_addr(2:1)&$f) + 48 + ((mac_addr(2:1)&$f )/10*7)
    tmp(11:1) = ":"
    tmp(12:1) = (mac_addr(1:1)/$10) + 48 + ((mac_addr(1:1)/$10)/10*7)
    tmp(13:1) = (mac_addr(1:1)&$f) + 48 + ((mac_addr(1:1)&$f )/10*7)
    tmp(14:1) = ":"
    tmp(15:1) = (mac_addr(0:1)/$10)+ 48 + ((mac_addr(0:1)/$10)/10*7)
    tmp(16:1) = (mac_addr(0:1)&$f) + 48 + ((mac_addr(0:1)&$f )/10*7)

    # Write 12-byte serial+MAC value to "Serial Number" characteristic
    call attributes_write(xgatt_serial_number, 0, 17, tmp(0:17))

    # Initialize variables
    dfu_pointer = 0
    connected = 0
    led_value = 0
    # configure P0.0 as output
    call hardware_io_port_config_direction(0, $01)
 
    #Set continuous timer to generate event every TIMER_SECONDS
    call hardware_set_soft_timer(TIMER_SECONDS * TICKS_PER_SECOND, MAIN_TIMER_HANDLE, 0)
    
    # Seed the random number generator using the last digits of the serial number 
    seed = (tmp(15) << 8) + tmp(16)
    call initialize_rand(seed)
    
    # For some extra randomness, can seed the rand generator using the ADC results from internal temperature
    # call hardware_adc_read(14, 3, 0)
end


event hardware_adc_result(input, value)
    if input = 14 then
        # Use ambient temperature check to augment seed
        seed = seed * (value & $ff)
        call initialize_rand(seed)
    end if
end


#Timer event listener toggles LED at a certain rate
event hardware_soft_timer(handle)

    call hardware_io_port_read(0, $01)(read_result, port, led_value)
    if (led_value & $01) then
        call hardware_io_port_write(0, $01, $00)
    else
        call hardware_io_port_write(0, $01, $01)
    end if

    # Generate a random number and return it to the debugging output
    call rand()
    tmp(0:4) = rand_number
    call attributes_write(xgatt_debug, 0, 4, tmp(0:4))
    
    # Generate a random number, put it into a bucket based on probability, and return it to the debugging output
    # call rand()
    # if rand_number <= 536870912 then  # 25% of the time
        # read_result = 1
    # end if
            
    # if rand_number > 536870912 && rand_number <= 1073741824 then  # 25% of the time
        # read_result = 2
    # end if

    # if rand_number > 1073741824 && rand_number <= 1825361100 then  # 35% of the time
        # read_result = 3
    # end if

    # if rand_number > 1825361100 then  # 15% of the time
        # read_result = 4
    # end if

    # tmp(0:4) = read_result
    # call attributes_write(xgatt_debug, 0, 4, tmp(0:4))
end


event connection_status(connection, flags, address, address_type, conn_interval, timeout, latency, bonding)
    if connected = 0 then
        # Set up a connection interval of between 6*1.25ms to 10*1.25ms (7.5-12.5ms)
        # Android (and iOS?) respond to connection interval updates, but cannot set them
        call connection_update(connection, 6, 10, latency, timeout)
    end if
    connected = 1
end


# Disconnection event listener
event connection_disconnected(handle, result)
    connected = 0
    # Makes the device visible and connectable
    call gap_set_mode(gap_general_discoverable, gap_undirected_connectable)
end
